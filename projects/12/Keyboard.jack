// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        let c = Keyboard.raw_readChar();
        do Output.printChar(c);
        return c;
    }

    function char raw_readChar() {
        var char c;
        do Keyboard.displayCursor();
        let c = Keyboard.keyPressed();
        while( c=0 ){
            let c = Keyboard.keyPressed();
        }
        while( ~(Keyboard.keyPressed() = 0) ){
        }
        return c;
    }

    function void displayCursor(){
        // do Output.printChar(0);
        // do Output.backSpace();
        return;
    }

    function bool printable(char c){
        if( (c > 31) & (c < 127) ){
            return true;
        }
        return false;        
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char c;
        if(message.length() > 0){
            do Output.printString(message);
        }
        let s = String.new(64);
        let c = Keyboard.raw_readChar();
        while( ~(c = String.newLine()) ){
            if( Keyboard.printable(c) ){
                do s.appendChar(c);
                do Output.printChar(c);
            }else{
                if( (c = String.backSpace()) & (s.length() > 0) ){
                    do s.eraseLastChar();
                    do Output.backSpace();
                }
            }
            let c = Keyboard.raw_readChar();
        }
        do Output.println();
        return s;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var int i;
        var String s;
        let s = Keyboard.readLine(message);
        let i = s.intValue();
        return i;
    }
}
